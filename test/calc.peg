
Stmt    = - e:Expr EOL                  { Debug.Print $e }
        | ( !EOL . )* EOL               { Debug.Print "error" }

Expr    = i:ID ASSIGN s:Sum             { .vars($i) = $s : $$ = $s }
        | s:Sum                         { $$ = $s }

Sum     = l:Product
                ( PLUS  r:Product       { $l = $l + $r }
                | MINUS r:Product       { $l = $l - $r }
                )*                      { $$ = $l }

Product = l:Value
                ( TIMES  r:Value        { $l = $l * $r }
                | DIVIDE r:Value        { $l = $l / $r }
                )*                      { $$ = $l }

Value   = i:NUMBER                      { $$ = CLng($$text) }
        | i:ID !ASSIGN                  { $$ = .vars($i) }
        | OPEN i:Expr CLOSE             { $$ = $i }

NUMBER  = < [0-9]+ >    -               { $$ = CLng($$text) }
ID      = < [a-z]  >    -               { $$ = Asc($$text) }
ASSIGN  = '='           -
PLUS    = '+'           -
MINUS   = '-'           -
TIMES   = '*'           -
DIVIDE  = '/'           -
OPEN    = '('           -
CLOSE   = ')'           -

-       = [ \t]*
EOL     = '\n' | '\r\n' | '\r' | ';'
